type managedIdentitiesType = {
  @description('Optional. Enables system assigned managed identity on the resource.')
  systemAssigned: bool?

  @description('Optional. The resource ID(s) to assign to the resource. Required if a user assigned identity is used for encryption.')
  userAssignedResourceIds: string[]?
}?

@description('Optional. The managed identity definition for this resource.')
param managedIdentities managedIdentitiesType

var formattedUserAssignedIdentities = reduce(map((managedIdentities.?userAssignedResourceIds ?? []), (id) => { '${id}': {} }), {}, (cur, next) => union(cur, next)) // Converts the flat array to an object like { '${id1}': {}, '${id2}': {} }
var identity = !empty(managedIdentities) ? {
  type: (managedIdentities.?systemAssigned ?? false) ? (!empty(managedIdentities.?userAssignedResourceIds ?? {}) ? 'SystemAssigned,UserAssigned' : 'SystemAssigned') : (!empty(managedIdentities.?userAssignedResourceIds ?? {}) ? 'UserAssigned' : null)
  userAssignedIdentities: !empty(formattedUserAssignedIdentities) ? formattedUserAssignedIdentities : null
} : null

resource exampleResource 'Example.Resource/example@2023-01-31' = {
  name: 'exampleResource'
  identity: identity
  properties: {
    ... // other properties
  }
}

@description('The principal ID of the system assigned identity.')
output systemAssignedPrincipalId string = (managedIdentities.?systemAssigned ?? false) && contains(exampleResource.identity, 'principalId') ? exampleResource.identity.principalId : ''
